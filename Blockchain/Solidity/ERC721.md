# ERC721

## OpenZeppelin

Docs: [https://docs.openzeppelin.com/contracts/4.x/api/token/erc721](https://docs.openzeppelin.com/contracts/4.x/api/token/erc721)

Wizard: [https://docs.openzeppelin.com/contracts/4.x/wizard](https://docs.openzeppelin.com/contracts/4.x/wizard)

```solidity
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (token/ERC721/ERC721.sol)

pragma solidity ^0.8.0;

import "./IERC721.sol";
import "./IERC721Receiver.sol";
import "./extensions/IERC721Metadata.sol";
import "../../utils/Address.sol";
import "../../utils/Context.sol";
import "../../utils/Strings.sol";
import "../../utils/introspection/ERC165.sol";

/**
 * @dev Implementation of https://eips.ethereum.org/EIPS/eip-721[ERC721] Non-Fungible Token Standard, including
 * the Metadata extension, but not including the Enumerable extension, which is available separately as
 * {ERC721Enumerable}.
 */
contract ERC721 is Context, ERC165, IERC721, IERC721Metadata {
    using Address for address;
    using Strings for uint256;

    // Token name
    string private _name;

    // Token symbol
    string private _symbol;

    // Mapping from token ID to owner address
    mapping(uint256 => address) private _owners;

    // Mapping owner address to token count
    mapping(address => uint256) private _balances;

    // Mapping from token ID to approved address
    mapping(uint256 => address) private _tokenApprovals;

    // Mapping from owner to operator approvals
    mapping(address => mapping(address => bool)) private _operatorApprovals;

    /**
     * @dev Initializes the contract by setting a `name` and a `symbol` to the token collection.
     */
    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {
        return
            interfaceId == type(IERC721).interfaceId ||
            interfaceId == type(IERC721Metadata).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    /**
     * @dev See {IERC721-balanceOf}.
     */
    function balanceOf(address owner) public view virtual override returns (uint256) {
        require(owner != address(0), "ERC721: address zero is not a valid owner");
        return _balances[owner];
    }

    /**
     * @dev See {IERC721-ownerOf}.
     */
    function ownerOf(uint256 tokenId) public view virtual override returns (address) {
        address owner = _owners[tokenId];
        require(owner != address(0), "ERC721: invalid token ID");
        return owner;
    }

    /**
     * @dev See {IERC721Metadata-name}.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev See {IERC721Metadata-symbol}.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev See {IERC721Metadata-tokenURI}.
     */
    function tokenURI(uint256 tokenId) public view virtual override returns (string memory) {
        _requireMinted(tokenId);

        string memory baseURI = _baseURI();
        return bytes(baseURI).length > 0 ? string(abi.encodePacked(baseURI, tokenId.toString())) : "";
    }

    /**
     * @dev Base URI for computing {tokenURI}. If set, the resulting URI for each
     * token will be the concatenation of the `baseURI` and the `tokenId`. Empty
     * by default, can be overridden in child contracts.
     */
    function _baseURI() internal view virtual returns (string memory) {
        return "";
    }

    /**
     * @dev See {IERC721-approve}.
     */
    function approve(address to, uint256 tokenId) public virtual override {
        address owner = ERC721.ownerOf(tokenId);
        require(to != owner, "ERC721: approval to current owner");

        require(
            _msgSender() == owner || isApprovedForAll(owner, _msgSender()),
            "ERC721: approve caller is not token owner nor approved for all"
        );

        _approve(to, tokenId);
    }

    /**
     * @dev See {IERC721-getApproved}.
     */
    function getApproved(uint256 tokenId) public view virtual override returns (address) {
        _requireMinted(tokenId);

        return _tokenApprovals[tokenId];
    }

    /**
     * @dev See {IERC721-setApprovalForAll}.
     */
    function setApprovalForAll(address operator, bool approved) public virtual override {
        _setApprovalForAll(_msgSender(), operator, approved);
    }

    /**
     * @dev See {IERC721-isApprovedForAll}.
     */
    function isApprovedForAll(address owner, address operator) public view virtual override returns (bool) {
        return _operatorApprovals[owner][operator];
    }

    /**
     * @dev See {IERC721-transferFrom}.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        //solhint-disable-next-line max-line-length
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: caller is not token owner nor approved");

        _transfer(from, to, tokenId);
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        safeTransferFrom(from, to, tokenId, "");
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes memory data
    ) public virtual override {
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: caller is not token owner nor approved");
        _safeTransfer(from, to, tokenId, data);
    }

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * `data` is additional data, it has no specified format and it is sent in call to `to`.
     *
     * This internal function is equivalent to {safeTransferFrom}, and can be used to e.g.
     * implement alternative mechanisms to perform token transfer, such as signature-based.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function _safeTransfer(
        address from,
        address to,
        uint256 tokenId,
        bytes memory data
    ) internal virtual {
        _transfer(from, to, tokenId);
        require(_checkOnERC721Received(from, to, tokenId, data), "ERC721: transfer to non ERC721Receiver implementer");
    }

    /**
     * @dev Returns whether `tokenId` exists.
     *
     * Tokens can be managed by their owner or approved accounts via {approve} or {setApprovalForAll}.
     *
     * Tokens start existing when they are minted (`_mint`),
     * and stop existing when they are burned (`_burn`).
     */
    function _exists(uint256 tokenId) internal view virtual returns (bool) {
        return _owners[tokenId] != address(0);
    }

    /**
     * @dev Returns whether `spender` is allowed to manage `tokenId`.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function _isApprovedOrOwner(address spender, uint256 tokenId) internal view virtual returns (bool) {
        address owner = ERC721.ownerOf(tokenId);
        return (spender == owner || isApprovedForAll(owner, spender) || getApproved(tokenId) == spender);
    }

    /**
     * @dev Safely mints `tokenId` and transfers it to `to`.
     *
     * Requirements:
     *
     * - `tokenId` must not exist.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function _safeMint(address to, uint256 tokenId) internal virtual {
        _safeMint(to, tokenId, "");
    }

    /**
     * @dev Same as {xref-ERC721-_safeMint-address-uint256-}[`_safeMint`], with an additional `data` parameter which is
     * forwarded in {IERC721Receiver-onERC721Received} to contract recipients.
     */
    function _safeMint(
        address to,
        uint256 tokenId,
        bytes memory data
    ) internal virtual {
        _mint(to, tokenId);
        require(
            _checkOnERC721Received(address(0), to, tokenId, data),
            "ERC721: transfer to non ERC721Receiver implementer"
        );
    }

    /**
     * @dev Mints `tokenId` and transfers it to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {_safeMint} whenever possible
     *
     * Requirements:
     *
     * - `tokenId` must not exist.
     * - `to` cannot be the zero address.
     *
     * Emits a {Transfer} event.
     */
    function _mint(address to, uint256 tokenId) internal virtual {
        require(to != address(0), "ERC721: mint to the zero address");
        require(!_exists(tokenId), "ERC721: token already minted");

        _beforeTokenTransfer(address(0), to, tokenId);

        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(address(0), to, tokenId);

        _afterTokenTransfer(address(0), to, tokenId);
    }

    /**
     * @dev Destroys `tokenId`.
     * The approval is cleared when the token is burned.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     *
     * Emits a {Transfer} event.
     */
    function _burn(uint256 tokenId) internal virtual {
        address owner = ERC721.ownerOf(tokenId);

        _beforeTokenTransfer(owner, address(0), tokenId);

        // Clear approvals
        _approve(address(0), tokenId);

        _balances[owner] -= 1;
        delete _owners[tokenId];

        emit Transfer(owner, address(0), tokenId);

        _afterTokenTransfer(owner, address(0), tokenId);
    }

    /**
     * @dev Transfers `tokenId` from `from` to `to`.
     *  As opposed to {transferFrom}, this imposes no restrictions on msg.sender.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     *
     * Emits a {Transfer} event.
     */
    function _transfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {
        require(ERC721.ownerOf(tokenId) == from, "ERC721: transfer from incorrect owner");
        require(to != address(0), "ERC721: transfer to the zero address");

        _beforeTokenTransfer(from, to, tokenId);

        // Clear approvals from the previous owner
        _approve(address(0), tokenId);

        _balances[from] -= 1;
        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(from, to, tokenId);

        _afterTokenTransfer(from, to, tokenId);
    }

    /**
     * @dev Approve `to` to operate on `tokenId`
     *
     * Emits an {Approval} event.
     */
    function _approve(address to, uint256 tokenId) internal virtual {
        _tokenApprovals[tokenId] = to;
        emit Approval(ERC721.ownerOf(tokenId), to, tokenId);
    }

    /**
     * @dev Approve `operator` to operate on all of `owner` tokens
     *
     * Emits an {ApprovalForAll} event.
     */
    function _setApprovalForAll(
        address owner,
        address operator,
        bool approved
    ) internal virtual {
        require(owner != operator, "ERC721: approve to caller");
        _operatorApprovals[owner][operator] = approved;
        emit ApprovalForAll(owner, operator, approved);
    }

    /**
     * @dev Reverts if the `tokenId` has not been minted yet.
     */
    function _requireMinted(uint256 tokenId) internal view virtual {
        require(_exists(tokenId), "ERC721: invalid token ID");
    }

    /**
     * @dev Internal function to invoke {IERC721Receiver-onERC721Received} on a target address.
     * The call is not executed if the target address is not a contract.
     *
     * @param from address representing the previous owner of the given token ID
     * @param to target address that will receive the tokens
     * @param tokenId uint256 ID of the token to be transferred
     * @param data bytes optional data to send along with the call
     * @return bool whether the call correctly returned the expected magic value
     */
    function _checkOnERC721Received(
        address from,
        address to,
        uint256 tokenId,
        bytes memory data
    ) private returns (bool) {
        if (to.isContract()) {
            try IERC721Receiver(to).onERC721Received(_msgSender(), from, tokenId, data) returns (bytes4 retval) {
                return retval == IERC721Receiver.onERC721Received.selector;
            } catch (bytes memory reason) {
                if (reason.length == 0) {
                    revert("ERC721: transfer to non ERC721Receiver implementer");
                } else {
                    /// @solidity memory-safe-assembly
                    assembly {
                        revert(add(32, reason), mload(reason))
                    }
                }
            }
        } else {
            return true;
        }
    }

    /**
     * @dev Hook that is called before any token transfer. This includes minting
     * and burning.
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, ``from``'s `tokenId` will be
     * transferred to `to`.
     * - When `from` is zero, `tokenId` will be minted for `to`.
     * - When `to` is zero, ``from``'s `tokenId` will be burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {}

    /**
     * @dev Hook that is called after any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _afterTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {}
}
```

- 보통 스마트 컨트랙트를 작성할 때 `openzeppelin` 라이브러리 코드을 사용하는가?
    - 실제 현업에서 이더리움 스마트 컨트랙트를 작성할 때 `openzeppelin` 라이브러리를 사용한다고 한다.
    - 스마트 컨트랙트의 경우에는 코드를 작성하면 [**audit(오딧)**](https://velog.io/@jhin/컨트랙트-오딧audit)이라는 보안 감사 과정을 거쳐야 하는데, `openzeppelin` 라이브러리는 이 과정을 거친 검증된 코드이기 때문에 안정성이 높다.
    - Audit 과정 대한 설명: [https://velog.io/@jhin/컨트랙트-오딧audit](https://velog.io/@jhin/%EC%BB%A8%ED%8A%B8%EB%9E%99%ED%8A%B8-%EC%98%A4%EB%94%A7audit)
    - 기획에 따라 `openzeppelin` 라이브러리를 `customizing`해서 사용한다.
    - `customizing`의 가벼운 예시로는 PFP 민팅 개수 또는 권한을 제한하는 경우이다.

### EIP-721 요약

**Docs: [https://eips.ethereum.org/EIPS/eip-721](https://eips.ethereum.org/EIPS/eip-721)**

```solidity
* Interface ERC721

// 특정 메커니즘으로부터 특정 NFT의 소유권을 변경할 때 실행합니다.
event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);

// NFT에 대해 승인된 주소가 변경되거나 재확인될 때 실행합니다.
event Approval(address indexed _owner, address indexed _approved, uint256 indexed _tokenId);

// 운영자가 소유자에 대해 활성화 또는 비활성화할 경우 실행합니다.
event ApprovalForAll(address indexed _owner, address indexed _operator, bool _approved);

// 소유자에게 할당된 모든 NFT를 개수를 구합니다.
function balanceOf(address _owner) external view returns (uint256);

// NFT의 소유자를 찾습니다.
function ownerOf(uint256 _tokenId) external view returns (address);

// 특정 주소에서 다른 주소로 NFT의 소유권을 이전합니다.
function safeTransferFrom(address _from, address _to, uint256 _tokenId, bytes data) external payable;

// 데이터 파라미터를 가진 다른 기능과 동일하게 동작합니다.
function safeTransferFrom(address _from, address _to, uint256 _tokenId) external payable;

// NFT의 소유권을 이전합니다
function transferFrom(address _from, address _to, uint256 _tokenId) external payable;

// NFT에 대해 승인된 주소를 바꾸거나 재확인합니다.
function approve(address _approved, uint256 _tokenId) external payable;

// ’msg.sender’의 모든 자산을 관리하도록 제3자(‘운영자’)를 승인하거나 승인 취소합니다.
function setApprovalForAll(address _operator, bool _approved) external;

// 단일 NFT에 대해 승인된 주소를 불러옵니다.
function getApproved(uint256 _tokenId)  external view returns (address);

// 주소가 어떤 주소에 대해 승인된 운영자인지 확인합니다.
function isApprovedForAll(address _owner, address _operator) external view returns (bool);

* Interface ERC165

// 계약이 인터페이스를 구현하는지 확인합니다.
function supportsInterface(bytes4 interfaceID) external view returns (bool);

* Interface ERC721TokenReceiver

// ERC721 스마트 컨트랙트에서는 ‘transfer’ 이후에 받는 쪽이 이 기능을 호출합니다.
function onERC721Received(address _operator, address _from, uint256 _tokenId, bytes _data) external returns(bytes4);

* Interface ERC721Metadata

// 해당 컨트랙트에 있는 NFT 컬렉션에 대한 이름을 출력합니다.
function name() external view returns (string _name);

// 해당 컨트랙트에 있는 NFT에 대한 축약된 이름을 출력합니다.
function symbol() external view returns (string _symbol);

// 주어진 자산에 대한 개별 URI를 출력합니다.
function tokenURI(uint256 _tokenId) external view returns (string);

* Interface ERC721Enumerable

// 해당 컨트랙트에서 추적하는 NFT 개수를 출력합니다.
function totalSupply() external view returns (uint256);

// 유효한 NFT를 계산합니다.
function tokenByIndex(uint256 _index) external view returns (uint256);

// 소유자가 할당된 NFT를 계산합니다.
function tokenOfOwnerByIndex(address _owner, uint256 _index) external view returns (uint256);
```

### 궁금한 부분

- `ownerOf` 함수는 왜 `ERC721.ownerOf()`로 사용할까?
    - [https://ethereum.stackexchange.com/questions/112782/why-is-erc721-ownerof-function-referenced-differently-from-all-other-function-in](https://ethereum.stackexchange.com/questions/112782/why-is-erc721-ownerof-function-referenced-differently-from-all-other-function-in)
- `ERC165`가 필요한 이유?
    - ERC-165 표준: [https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]](https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified%5BEIP%20section%5D)
    - `Smart Contract`가 구현하는 인터페이스를 게시하고 감지하는 표준 방법을 만든다.
- `onERC721Received`는 어떤 역할을 하는가?
    - EOA가 아닌 CA에 Token을 전송할 때 전송 받는 CA에 `onERC721Received`가 구현되어 있어야 한다.
    
    ```solidity
    interface IERC721Receiver {
        /**
         * @dev Whenever an {IERC721} `tokenId` token is transferred to this contract via {IERC721-safeTransferFrom}
         * by `operator` from `from`, this function is called.
         *
         * It must return its Solidity selector to confirm the token transfer.
         * If any other value is returned or the interface is not implemented by the recipient, the transfer will be reverted.
         *
         * The selector can be obtained in Solidity with `IERC721Receiver.onERC721Received.selector`.
         */
        function onERC721Received(
            address operator,
            address from,
            uint256 tokenId,
            bytes calldata data
        ) external returns (bytes4);
    }
    ```
    
    - 안전하게 전송하기 위해 `_checkOnERC721Received`으로 전송 받는 CA에 `onERC721Received` 가 구현되어 있는지 확인한다.
    
    ```solidity
    	
    function _safeTransfer(
        address from,
        address to,
        uint256 tokenId
        bytes memory data
    ) internal virtual {
        _transfer(from, to, tokenId);
        require(_checkOnERC721Received(from, to, tokenId, data), "ERC721: transfer to non ERC721Receiver implementer");
    }
    
    function _checkOnERC721Received(
        address from,
        address to,
        uint256 tokenId,
        bytes memory data
    ) private returns (bool) {
        if (to.isContract()) {
            try IERC721Receiver(to).onERC721Received(_msgSender(), from, tokenId, data) returns (bytes4 retval) {
                return retval == IERC721Receiver.onERC721Received.selector;
            } catch (bytes memory reason) {
                if (reason.length == 0) {
                    revert("ERC721: transfer to non ERC721Receiver implementer");
                } else {
                    /// @solidity memory-safe-assembly
                    assembly {
                        revert(add(32, reason), mload(reason))
                    }
                }
            }
        } else {
            return true;
        }
    }
    ```
    

## Solidity 문법

### Event

- `solidity`는 `transaction`을 완료하면 영수증을 발행한다. 이 영수증은 `transaction`의 실행동안 발생한 행위에 대한 정보를 제공하는 로그 엔트리를 가진다. 이벤트는 이러한 로그를 만들기 위해 사용하는 `solidity`의 고 수준 객체이다. 특정 이벤트가 일어나는지 감시해 사용자 인터페이스에 반영하거나 해당 `contract` 상의 이벤트에 대응되는 변화를 애플리케이션에 반영할 수 있다.
- 아래와 같이 인수를 취할 수 있다. 이것은 [](https://ahma.tistory.com/65)`Serialize` 되어 `blockchain` 상의 `transaction` 로그에 기록된다. 인수 앞에 `indexed` `keyword`를 붙여 애플리케이션에서 검색하거나 `filtering`할 수 있다.

```solidity
event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);
```

### Emit

- 아래와 같이 `transaction` 로그에 이벤트 데이터를 담을 수 있다.

```solidity
emit Transfer(address(0), to, tokenId);
```

### override & virtual

- `override`는 이 함수는 `overriding`한 함수라고 표시한 것이고, `virtual`은 앞으로 해당 함수를 `overriding`할 수 있다는 뜻이다.

```solidity
pragma solidity >=0.5.0 <0.7.0;

contract Base {
    function foo() virtual public {}
}

contract Middle is Base {}

contract Inherited is Middle {
    function foo() public override {}
}
```

- **Multiple Inheritance Example**

```solidity
pragma solidity >=0.5.0 <0.7.0;

contract Base1 {
    function foo() virtual public {}
}

contract Base2 {
    function foo() virtual public {}
}

contract Inherited is Base1, Base2 {
    // Derives from multiple bases defining foo(), so we must explicitly
    // override it
    function foo() public override(Base1, Base2) {}
}
```

### Storage & Memory

- `storage`는 `Blockchain` 상에 영구적으로 저장되는 변수이다.
- `memory`는 임시적으로 저장되는 변수로 `Contract` 함수에 대한 외부 호출이 일어나는 사이에 지워진다.
- 상태 변수(함수 외부에 선언된 변수)는 초기 설정상 `storage`로 선언되어 `Blockchain`에 영구적으로 저장되는 반면, 함수 내에 선언된 변수는 `memory`로 자동 선언되어서 함수 호출이 종료되면 사라진다.

### Internal과 External

- `public`과 `private` 이외에도 솔리디티에는 `internal`과 `external`이라는 함수 접근 제어자가 있다.
- `internal`은 함수가 정의된 컨트랙트를 상속하는 컨트랙트에서도 접근이 가능하다.
- `external`은 함수가 컨트랙트 바깥에서만 호출될 수 있고 컨트랙트 내의 다른 함수에 의해 호출될 수 없다.
